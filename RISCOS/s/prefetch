    AREA |Asm$$Code|, CODE, READONLY

    EXPORT prefetch_handler
    EXPORT get_prefetch_handler_workspace

; MRC     p15, #0, r0, c6, c0, #2   ; read IFAR
; MRC     p15, #0, r1, c5, c0, #1   ; read IFSR

IFSR_FS_MASK    * (1<<10) :OR: (2_1111<<0)
IFSR_FS_DEBUG   * (0<<10) :OR: (2_0010<<0)

MODE_MASK       * 2_11111
MODE_SVC32      * 2_10011

PSR_I           * 1<<7

OS_WriteS       * 0x20001
OS_NewLine      * 0x20003
OS_CLI          * 0x20005

; Prefetch handler is entered in ABT mode.
; SPSR contains the old mode and processor state.
; sp_abt is in use.
; sp_<mode> contains the sp from the old mode
; IFAR contains the faulting address
; IFSR contains the status, which for breakpoint will be
;    (0<<10) | (%0010<<0)
; r0-r12 contain the registers at time of fault.
prefetch_handler
    STMFD   sp!, {r0, r14}

    MRC     p15, 0, r0, c5, c0, 1       ; r0 = IFSR
    LDR     r14, =IFSR_FS_MASK
    AND     r0, r0, r14                 ; r0 = FS from IFSR
    TEQ     r0, #IFSR_FS_DEBUG
    BEQ     is_a_breakpoint

    LDMFD   sp!, {r0, r14}
    LDR     pc, prefetch_handler_workspace + 0

is_a_breakpoint
;    MOV     pc, #0                      ; temporary crash

    LDR     r0, prefetch_handler_workspace + 4
    STMIB   r0, {r1-r12}
    LDR     r1, [sp]                    ; old register 0
    STR     r1, [r0]                    ; store in exception dump area

    MRS     r1, spsr
    STR     r1, [r0, #16*4]             ; Store SPSR at the end of the register dump

    MRC     p15, 0, r0, c6, c0, 2       ; read IFAR (the pc at the time of the abort)
    STR     r0, [r0, #15*4]             ; store in the exception dump

    ADD     sp, sp, #4*2                ; unstack the ABT preserved registers

; need to go to SVC mode in order to call our module code.
    MRS     r1, cpsr
    BIC     r1, r1, #MODE_MASK
    ORR     r1, r1, #MODE_SVC32         ; FIXME: Should probably be SVC<bitness>
    BIC     r1, r1, #PSR_I              ; enable interrupts
    MSR     cpsr_cxsf, r1

; Now in SVC32
; FIXME: This should probably be the stack sequence used by Classic RISC OS
    STMFD   sp!, {r0, r14}

    STR     r14, [r0, #14*4]            ; store R14_svc in exception dump
    ; FIXME: May have come from USR mode, not SVC mode.
    ADD     r1, sp, #4*2
    STR     r1, [r0, #13*4]             ; store old R13_svc in exception dump

    SWI     OS_WriteS
    = "Hit a breakpoint!",0
    ALIGN
    SWI     OS_NewLine

; FIXME : We should probably call *Debug here; or something similar.
    ADR     r0, gos
    SWI     OS_CLI

gos
    = "GOS",0
    ALIGN


prefetch_handler_workspace
    DCD     0                           ; old_prefetch_handler
    DCD     0                           ; exception dump area (16 + 1 words)

get_prefetch_handler_workspace
    ADR     r0, prefetch_handler_workspace
    MOV     pc, lr




    END
